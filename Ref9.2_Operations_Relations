Declaration:  Dom : (A ↔ B) → set A
Declaration:  Ran : (A ↔ B) → set B

Axiom (14.16b) “Membership in `Dom`”:
  x ∈ Dom R  ≡  ∃ y • x ⦗ R ⦘ y

Axiom (14.17b) “Membership in `Ran`”:
  y ∈ Ran R  ≡  ∃ x • x ⦗ R ⦘ y

Theorem (14.19a) “Domain of converse”: Dom (R ˘) = Ran R
Proof:
  Using “Set extensionality”:
    Subproof for `∀ x • x ∈ Dom (R ˘) ≡ x ∈ Ran R`:
      For any `x`:
          x ∈ Dom (R ˘)
        =⟨ “Membership in `Dom`” ⟩
          ∃ y • x ⦗ R ˘ ⦘ y
        =⟨ “Relation converse” ⟩
          ∃ y • y ⦗ R ⦘ x
        =⟨ “Membership in `Ran`” ⟩
          x ∈ Ran R
        
Theorem (14.19b) “Range of converse”: Ran (R ˘) = Dom R
Proof:
  Using “Set extensionality”:
    Subproof for `∀ x • x ∈ Ran (R ˘) ≡ x ∈ Dom R`:
      For any `x`:
          x ∈ Ran (R ˘)
        =⟨ “Membership in `Ran`” ⟩
          ∃ y • y ⦗ R ˘ ⦘ x
        =⟨ “Relation converse” ⟩
          ∃ y • x ⦗ R ⦘ y
        =⟨ “Membership in `Dom`” ⟩
          x ∈ Dom R

Theorem (14.101) “Domain of union”:
    Dom (R ∪ S) = Dom R ∪ Dom S
Proof:
  Using “Set extensionality”:
    For any `x`:
        x ∈ Dom (R ∪ S)
      =⟨ “Membership in `Dom`” ⟩
        ∃ y • x ⦗ R ∪ S ⦘ y
      =⟨ “Relation union” ⟩
        ∃ y • x ⦗ R ⦘ y ∨ x ⦗ S ⦘ y
      =⟨ “Distributivity of ∃ over ∨” ⟩  
        (∃ y • x ⦗ R ⦘ y) ∨ (∃ y • x ⦗ S ⦘ y)
      =⟨ “Membership in `Dom`” ⟩ 
        x ∈ Dom R ∨ x ∈ Dom S
      =⟨ “Union” ⟩ 
        x ∈ Dom R ∪ Dom S

Theorem (14.102) “Range of union”:
    Ran (R ∪ S) = Ran R ∪ Ran S
Proof:
  Using “Set extensionality”:
    For any `x`:
        x ∈ Ran (R ∪ S)
      =⟨ “Membership in `Ran`” ⟩
        ∃ y • y ⦗ R ∪ S ⦘ x
      =⟨ “Relation union” ⟩
        ∃ y • y ⦗ R ⦘ x ∨ y ⦗ S ⦘ x
      =⟨ “Distributivity of ∃ over ∨” ⟩  
        (∃ y • y ⦗ R ⦘ x) ∨ (∃ y • y ⦗ S ⦘ x)
      =⟨ “Membership in `Ran`” ⟩ 
        x ∈ Ran R ∨ x ∈ Ran S
      =⟨ “Union” ⟩ 
        x ∈ Ran R ∪ Ran S

Theorem (14.103) “Domain of intersection”:
    Dom (R ∩ S) ⊆ Dom R ∩ Dom S
Proof:
  Using “Set inclusion”:
    For any `x`:
        x ∈ Dom (R ∩ S)
      =⟨ “Membership in `Dom`” ⟩
        ∃ y • x ⦗ R ∩ S ⦘ y
      =⟨ “Relation intersection” ⟩
        ∃ y • x ⦗ R ⦘ y ∧ x ⦗ S ⦘ y
      =⟨ “Idempotency of ∧” ⟩
        (∃ y • x ⦗ R ⦘ y ∧ x ⦗ S ⦘ y) ∧ (∃ y • x ⦗ R ⦘ y ∧ x ⦗ S ⦘ y)
      ⇒⟨ “Monotonicity of ∧” with 
         “Monotonicity of ∃” with
         “Weakening” (3.76b) ⟩   
        (∃ y • x ⦗ R ⦘ y) ∧ (∃ y • x ⦗ S ⦘ y)
      =⟨ “Membership in `Dom`” ⟩   
        x ∈ Dom R ∧ x ∈ Dom S
      =⟨ “Intersection” ⟩ 
        x ∈ Dom R ∩ Dom S

Theorem (14.104) “Range of intersection”:
    Ran (R ∩ S) ⊆ Ran R ∩ Ran S
Proof:
  Using “Set inclusion”:
    For any `x`:
        x ∈ Ran (R ∩ S)
      =⟨ “Membership in `Ran`” ⟩
        ∃ y • y ⦗ R ∩ S ⦘ x
      =⟨ “Relation intersection” ⟩
        ∃ y • y ⦗ R ⦘ x ∧ y ⦗ S ⦘ x
      =⟨ “Idempotency of ∧” ⟩
        (∃ y • y ⦗ R ⦘ x ∧ y ⦗ S ⦘ x) ∧ (∃ y • y ⦗ R ⦘ x ∧ y ⦗ S ⦘ x)
      ⇒⟨ “Monotonicity of ∧” with 
         “Monotonicity of ∃” with
         “Weakening” (3.76b) ⟩   
        (∃ y • y ⦗ R ⦘ x) ∧ (∃ y • y ⦗ S ⦘ x)
      =⟨ “Membership in `Ran`” ⟩   
        x ∈ Ran R ∧ x ∈ Ran S
      =⟨ “Intersection” ⟩ 
        x ∈ Ran R ∩ Ran S

Declaration: _◁_ , _⩤_ : set t₁ → (t₁ ↔ t₂) → (t₁ ↔ t₂)
Declaration: _▷_ , _⩥_ : (t₁ ↔ t₂) → set t₂ → (t₁ ↔ t₂)

Axiom (14.201) “Definition of ◁”:  A ◁ R = R ∩ (A × 𝐔)
Axiom (14.202) “Definition of ▷”:  R ▷ B = R ∩ (𝐔 × B)
Axiom (14.203) “Definition of ⩤”:  A ⩤ R = R ∩ (~ A × 𝐔)
Axiom (14.204) “Definition of ⩥”:  R ⩥ B = R ∩ (𝐔 × ~ B)

Lemma (14.205) “Definition of ⩤ via ◁”:   A ⩤ R  =  ~ A ◁ R
Proof:
    A ⩤ R 
  =⟨ “Definition of ⩤” ⟩
    R ∩ (~ A × 𝐔)
  =⟨ “Definition of ◁” ⟩ 
    ~ A ◁ R 

Lemma (14.206) “Definition of ⩥ via ▷”:   R ⩥ B  =  R ▷ ~ B
Proof:
    R ⩥ B
  =⟨ “Definition of ⩥” ⟩
    R ∩ (𝐔 × ~ B)
  =⟨ “Definition of ▷” ⟩
    R ▷ ~ B  

Theorem (14.207) “Distributivity of ◁ over relation intersection”:
    A ◁ (R ∩ S) = (A ◁ R) ∩ (A ◁ S)
Proof:
    (A ◁ R) ∩ (A ◁ S)
  =⟨ “Definition of ◁” ⟩
    (R ∩ (A × 𝐔)) ∩ (S ∩ (A × 𝐔))
  =⟨ “Idempotency of ∩” ⟩
    (R ∩ S) ∩ (A × 𝐔)
  =⟨ “Definition of ◁” ⟩
    A ◁ (R ∩ S)
 
Theorem (14.208) “Distributivity of ◁ over set intersection”:
    (A ∩ B) ◁ R = (A ◁ R) ∩ (B ◁ R)
Proof:
    (A ◁ R) ∩ (B ◁ R)
  =⟨ “Definition of ◁” ⟩
    (R ∩ (A × 𝐔)) ∩ (R ∩ (B × 𝐔))
  =⟨ “Idempotency of ∩” ⟩
    R ∩ (A × 𝐔) ∩ (B × 𝐔)
  =⟨ “Distributivity of × over ∩” ⟩ 
    R ∩ ((A ∩ B) × 𝐔)
  =⟨ “Definition of ◁” ⟩
    (A ∩ B) ◁ R

Theorem (14.209) “Distributivity of ◁ over relation union”:
    A ◁ (R ∪ S) = (A ◁ R) ∪ (A ◁ S)
Proof:
    (A ◁ R) ∪ (A ◁ S)
  =⟨ “Definition of ◁” ⟩ 
    (R ∩ (A × 𝐔)) ∪ (S ∩ (A × 𝐔))
  =⟨ “Distributivity of ∩ over ∪” ⟩
     (R ∪ S) ∩ (A × 𝐔)
  =⟨ “Definition of ◁” ⟩
     A ◁ (R ∪ S)

Theorem (14.210) “Distributivity of ◁ over set union”:
    (A ∪ B) ◁ R = (A ◁ R) ∪ (B ◁ R)
Proof:
    (A ◁ R) ∪ (B ◁ R)
  =⟨ “Definition of ◁” ⟩
    (R ∩ (A × 𝐔)) ∪ (R ∩ (B × 𝐔))
  =⟨ “Distributivity of ∩ over ∪” ⟩
    R ∩ ((A × 𝐔) ∪ (B × 𝐔))
  =⟨ “Distributivity of × over ∪” ⟩ 
    R ∩ ((A ∪ B) × 𝐔)
  =⟨ “Definition of ◁” ⟩
    (A ∪ B) ◁ R

Theorem (14.211) “Definition of ▷ via ◁”:  R ▷ B  =  (B ◁ R ˘) ˘
Proof:
    (B ◁ R ˘) ˘
  =⟨ “Definition of ◁” ⟩
    (R ˘ ∩ (B × 𝐔)) ˘
  =⟨ “Converse of ∩” ⟩
    (R ˘) ˘ ∩ (B × 𝐔) ˘
  =⟨ “Self-inverse of ˘” ⟩
     R ∩ (B × 𝐔) ˘
  =⟨ “Converse of ×” ⟩
     R ∩ (𝐔 × B)
  =⟨ “Definition of ▷” ⟩
     R ▷ B

Theorem (14.212) “Definition of ◁ via ▷”:  A ◁ R  =  (R ˘ ▷ A) ˘
Proof:
    (R ˘ ▷ A) ˘
  =⟨ “Definition of ▷” ⟩
    (R ˘ ∩ (𝐔 × A)) ˘
  =⟨ “Converse of ∩”, “Self-inverse of ˘”  ⟩
     R ∩ (𝐔 × A) ˘
  =⟨ “Converse of ×”  ⟩
     R ∩ (A × 𝐔)
  =⟨ “Definition of ◁” ⟩
     A ◁ R

Theorem (14.213) “Distributivity of ▷ over relation intersection”:
    (R ∩ S) ▷ B = (R ▷ B) ∩ (S ▷ B)
Proof:
    (R ∩ S) ▷ B
  =⟨ “Definition of ▷ via ◁” ⟩ 
    (B ◁ (R ∩ S) ˘) ˘
  =⟨ “Converse of ∩” ⟩
    (B ◁ (R ˘ ∩ S ˘)) ˘ 
  =⟨ “Distributivity of ◁ over relation intersection” ⟩
    ((B ◁ R ˘) ∩ (B ◁ S ˘)) ˘
  =⟨ “Converse of ∩” ⟩
    (B ◁ R ˘) ˘ ∩ (B ◁ S ˘) ˘
  =⟨ “Definition of ▷ via ◁” ⟩
    (R ▷ B) ∩ (S ▷ B)  

Theorem (14.214) “Distributivity of ▷ over set intersection”:
    R ▷ (B ∩ C) = (R ▷ B) ∩ (R ▷ C)
Proof:
    R ▷ (B ∩ C)
  =⟨ “Definition of ▷ via ◁” ⟩
    ((B ∩ C) ◁ R ˘) ˘
  =⟨ “Distributivity of ◁ over set intersection” ⟩
    ((B ◁ R ˘) ∩ (C ◁ R ˘)) ˘
  =⟨ “Converse of ∩” ⟩
    (B ◁ R ˘) ˘ ∩ (C ◁ R ˘) ˘
  =⟨ “Definition of ▷ via ◁” ⟩
    (R ▷ B) ∩ (R ▷ C)
   
Theorem (14.215) “Distributivity of ▷ over relation union”:
    (R ∪ S) ▷ B = (R ▷ B) ∪ (S ▷ B)
Proof:
    (R ∪ S) ▷ B
  =⟨ “Definition of ▷ via ◁” ⟩ 
    (B ◁ (R ∪ S) ˘) ˘
  =⟨ “Converse of ∪” , “Distributivity of ◁ over relation union” ⟩ 
    ((B ◁ R ˘) ∪ (B ◁ S ˘)) ˘
  =⟨ “Converse of ∪” ⟩
    ((B ◁ R ˘) ˘ ∪ (B ◁ S ˘) ˘)
  =⟨ “Definition of ▷ via ◁” ⟩  
     (R ▷ B) ∪ (S ▷ B)

Theorem (14.216) “Distributivity of ▷ over set union”:
    R ▷ (B ∪ C) = (R ▷ B) ∪ (R ▷ C)
Proof:
    R ▷ (B ∪ C)
  =⟨ “Definition of ▷ via ◁” ⟩
    ((B ∪ C) ◁ R ˘ ) ˘
  =⟨ “Distributivity of ◁ over set union” ⟩
    ((B ◁ R ˘) ∪ (C ◁ R ˘)) ˘
  =⟨ “Converse of ∪” ⟩
    (B ◁ R ˘) ˘ ∪ (C ◁ R ˘) ˘
  =⟨ “Definition of ▷ via ◁” ⟩
    (R ▷ B) ∪ (R ▷ C)
  
Theorem (14.217) “Definition of ⩥ via ⩤”:  R ⩥ B  =  (B ⩤ R ˘) ˘
Proof:
    R ⩥ B
  =⟨ “Definition of ⩥ via ▷” ⟩ 
    R ▷ ~ B
  =⟨ “Definition of ▷ via ◁” ⟩ 
    (~ B ◁ R ˘) ˘
  =⟨ “Definition of ⩤ via ◁” ⟩
    (B ⩤ R ˘) ˘
     
Theorem (14.218) “Definition of ⩤ via ⩥”:  A ⩤ R  =  (R ˘ ⩥ A) ˘
Proof:
    A ⩤ R
  =⟨ “Definition of ⩤ via ◁” ⟩
    ~ A ◁ R
  =⟨ “Definition of ◁ via ▷” ⟩ 
    (R ˘ ▷ ~ A) ˘ 
  =⟨ “Definition of ⩥ via ▷” ⟩ 
    (R ˘ ⩥ A) ˘ 

Theorem (14.219) “Distributivity of ⩤ over relation intersection”:
    A ⩤ (R ∩ S) = (A ⩤ R) ∩ (A ⩤ S)
Proof:
    A ⩤ (R ∩ S)
  =⟨ “Definition of ⩤ via ◁” ⟩
    ~ A ◁ (R ∩ S)
  =⟨ “Distributivity of ◁ over relation intersection” ⟩
    (~ A ◁ R) ∩ (~ A ◁ S)
  =⟨ “Definition of ⩤ via ◁” ⟩ 
    (A ⩤ R) ∩ (A ⩤ S) 

Theorem (14.220) “Distributivity of ⩤ over relation union”:
    A ⩤ (R ∪ S) = (A ⩤ R) ∪ (A ⩤ S)
Proof:
    A ⩤ (R ∪ S)
  =⟨ “Definition of ⩤ via ◁” ⟩
    ~ A ◁ (R ∪ S)
  =⟨ “Distributivity of ◁ over relation union” ⟩
    (~ A ◁ R) ∪ (~ A ◁ S)
  =⟨ “Definition of ⩤ via ◁” ⟩ 
    (A ⩤ R) ∪ (A ⩤ S) 

Theorem (14.221) “Distributivity of ⩥ over relation intersection”:
    (R ∩ S) ⩥ B = (R ⩥ B) ∩ (S ⩥ B)
Proof:
    (R ∩ S) ⩥ B
  =⟨ “Definition of ⩥ via ▷” ⟩
    (R ∩ S) ▷ ~ B
  =⟨ “Distributivity of ▷ over relation intersection” ⟩
    (R ▷ ~ B) ∩ (S ▷ ~ B)
  =⟨ “Definition of ⩥ via ▷” ⟩
    (R ⩥ B) ∩ (S ⩥ B)
   
Theorem (14.222) “Distributivity of ⩥ over relation union”:
    (R ∪ S) ⩥ B = (R ⩥ B) ∪ (S ⩥ B)
Proof:
    (R ∪ S) ⩥ B
  =⟨ “Definition of ⩥ via ▷” ⟩
    (R ∪ S) ▷ ~ B
  =⟨ “Distributivity of ▷ over relation union” ⟩
    (R ▷ ~ B) ∪ (S ▷ ~ B)
  =⟨ “Definition of ⩥ via ▷” ⟩
    (R ⩥ B) ∪ (S ⩥ B)

Theorem (14.223) “Co-distributivity of ⩤ over set intersection”:
    (A ∩ B) ⩤ R = (A ⩤ R) ∪ (B ⩤ R)
Proof:
    (A ∩ B) ⩤ R
  =⟨ “Definition of ⩤ via ◁” ⟩
    ~ (A ∩ B) ◁ R 
  =⟨ “De Morgan for ∩” ⟩
    (~ A ∪ ~ B) ◁ R 
  =⟨ “Distributivity of ◁ over set union” ⟩
    (~ A ◁ R) ∪ (~ B ◁ R)
  =⟨ “Definition of ⩤ via ◁” ⟩ 
    (A ⩤ R) ∪ (B ⩤ R)

Theorem (14.224) “Co-distributivity of ⩤ over set union”:
    (A ∪ B) ⩤ R = (A ⩤ R) ∩ (B ⩤ R)
Proof:
    (A ∪ B) ⩤ R
  =⟨ “Definition of ⩤ via ◁” ⟩
    ~ (A ∪ B) ◁ R 
  =⟨ “De Morgan for ∪” ⟩
    (~ A ∩ ~ B) ◁ R 
  =⟨ “Distributivity of ◁ over set intersection” ⟩
    (~ A ◁ R) ∩ (~ B ◁ R)
  =⟨ “Definition of ⩤ via ◁” ⟩ 
    (A ⩤ R) ∩ (B ⩤ R)

Theorem (14.225) “Co-distributivity of ⩥ over set intersection”:
    R ⩥ (B ∩ C) = (R ⩥ B) ∪ (R ⩥ C)
Proof:
    R ⩥ (B ∩ C)
  =⟨ “Definition of ⩥ via ▷” ⟩
    R ▷ ~ (B ∩ C)
  =⟨ “De Morgan for ∩” ⟩
    R ▷ (~ B ∪ ~ C)
  =⟨ “Distributivity of ▷ over set union” ⟩
    (R ▷ ~ B) ∪ (R ▷ ~ C)
  =⟨ “Definition of ⩥ via ▷” ⟩
    (R ⩥ B) ∪ (R ⩥ C)
    
Theorem (14.226) “Co-distributivity of ⩥ over set union”:
    R ⩥ (B ∪ C) = (R ⩥ B) ∩ (R ⩥ C)
Proof:
    R ⩥ (B ∪ C)
  =⟨ “Definition of ⩥ via ▷” ⟩
    R ▷ ~ (B ∪ C)
  =⟨ “De Morgan for ∪” ⟩
    R ▷ (~ B ∩ ~ C)
  =⟨ “Distributivity of ▷ over set intersection” ⟩
    (R ▷ ~ B) ∩ (R ▷ ~ C)
  =⟨ “Definition of ⩥ via ▷” ⟩
    (R ⩥ B) ∩ (R ⩥ C)

Theorem (14.227) “Relationship via ◁” “Domain restriction”:
    x ⦗ A ◁ R ⦘ y  ≡  x ∈ A ∧ x ⦗ R ⦘ y
Proof:
    x ⦗ A ◁ R ⦘ y
  =⟨ “Definition of ◁” ⟩
    x ⦗ R ∩ (A × 𝐔) ⦘ y
  =⟨ “Relation intersection”, “Relationship via ×” ⟩
    x ⦗ R ⦘ y ∧ x ∈ A ∧ y ∈ 𝐔
  =⟨ “Universal set”, “Identity of ∧” ⟩
    x ⦗ R ⦘ y ∧ x ∈ A 
    
Theorem (14.228) “Relationship via ▷” “Range restriction”:
    x ⦗ R ▷ B ⦘ y  ≡  x ⦗ R ⦘ y ∈ B
Proof:
    x ⦗ R ▷ B ⦘ y
  =⟨ “Definition of ▷” ⟩
    x ⦗ R ∩ (𝐔 × B) ⦘ y
  =⟨ “Relation intersection”, “Relationship via ×” ⟩
    x ⦗ R ⦘ y ∧ x ∈ 𝐔 ∧ y ∈ B
  =⟨ “Universal set”, “Identity of ∧” ⟩
    x ⦗ R ⦘ y ∧ y ∈ B 

Theorem (14.229) “Relationship via ⩤” “Domain antirestriction”:
    x ⦗ A ⩤ R ⦘ y  ≡  ¬ (x ∈ A) ∧ x ⦗ R ⦘ y
Proof:
    x ⦗ A ⩤ R ⦘ y
  =⟨ “Definition of ⩤ via ◁” ⟩
    x ⦗ ~ A ◁ R ⦘ y
  =⟨ “Relationship via ◁”, “Set complement” , “Definition of ≠” ⟩
    ¬ (x ∈ A) ∧ x ⦗ R ⦘ y
  
Theorem (14.230) “Relationship via ⩥” “Range antirestriction”:
    x ⦗ R ⩥ B ⦘ y  ≡  x ⦗ R ⦘ y ∧ ¬ (y ∈ B)
Proof:
    x ⦗ R ⩥ B ⦘ y
  =⟨ “Definition of ⩥ via ▷” ⟩
    x ⦗ R ▷ ~ B ⦘ y
  =⟨ “Relationship via ▷”, “Set complement” , “Definition of ≠” ⟩
    x ⦗ R ⦘ y ∧ ¬ (y ∈ B)

Theorem (14.231) “Domain of ◁”: Dom (A ◁ R) = A ∩ Dom R
Proof:
  Using “Set extensionality”:
    Subproof for `∀ x • x ∈ Dom (A ◁ R) ≡ x ∈ A ∩ Dom R`:
      For any `x`:
          x ∈ Dom (A ◁ R)
        =⟨ “Membership in `Dom`” ⟩
          ∃ y • x ⦗ A ◁ R ⦘ y
        =⟨ “Relationship via ◁” ⟩
          ∃ y • x ⦗ R ⦘ y ∧ x ∈ A
        =⟨ “Distributivity of ∧ over ∃” ⟩   
          x ∈ A ∧ ∃ y • x ⦗ R ⦘ y
        =⟨ “Membership in `Dom`” ⟩   
          x ∈ A ∧ x ∈ Dom R
        =⟨ “Intersection” ⟩
          x ∈ A ∩ Dom R
        
Theorem (14.232) “Range of ▷”: Ran (R ▷ B) = Ran R ∩ B
Proof:
  Using “Set extensionality”:
    Subproof for `∀ x • x ∈ Ran (R ▷ B) ≡ x ∈ Ran R ∩ B`:
      For any `x`:
          x ∈ Ran (R ▷ B)
        =⟨ “Membership in `Ran`” ⟩
          ∃ y • y ⦗ R ▷ B ⦘ x
        =⟨ “Relationship via ▷” ⟩
          ∃ y • y ⦗ R ⦘ x ∧ x ∈ B
        =⟨ “Distributivity of ∧ over ∃” ⟩
          (∃ y • y ⦗ R ⦘ x) ∧ x ∈ B
        =⟨ “Membership in `Ran`” , “Intersection” ⟩
          x ∈ Ran R ∩ B
           
Theorem (14.233) “Domain of ⩤”: Dom (A ⩤ R) = Dom R - A
Proof:
    Dom (A ⩤ R)
  =⟨ “Definition of ⩤ via ◁” ⟩
    Dom (~ A ◁ R)
  =⟨ “Domain of ◁” ⟩
    ~ A ∩ Dom R
  =⟨ “Set difference via ∩” ⟩
    Dom R - A   

Theorem (14.234) “Range of ⩥”: Ran (R ⩥ B) = Ran R - B
Proof:
    Ran (R ⩥ B)
  =⟨ “Definition of ⩥ via ▷” ⟩
    Ran (R ▷ ~ B)
  =⟨ “Range of ▷” ⟩
    Ran R ∩ ~ B
  =⟨ “Set difference via ∩” ⟩
    Ran R - B

Theorem (14.235) “Domain restriction by `Dom`”:  Dom S ◁ S = S
Proof:
  Using “Relation extensionality”:
    For any `x`, `y`:
        x ⦗ Dom S ◁ S ⦘ y
      =⟨ “Relationship via ◁” ⟩
        x ∈ Dom S ∧ x ⦗ S ⦘ y
      =⟨ “Membership in `Dom`” ⟩
        (∃ z • x ⦗ S ⦘ z) ∧ x ⦗ S ⦘ y
      =⟨ (3.60) ⟩
        x ⦗ S ⦘ y ≡ x ⦗ S ⦘ y ⇒ (∃ z • x ⦗ S ⦘ z) 
      =⟨ Substitution ⟩
        x ⦗ S ⦘ y ≡ (x ⦗ S ⦘ z)[z ≔ y] ⇒ (∃ z • x ⦗ S ⦘ z)
      =⟨ “∃-Introduction”, “Identity of ≡” ⟩ 
        x ⦗ S ⦘ y 

Theorem (14.236) “Range restriction by `Ran`”:  S ▷ Ran S = S
Proof:
  Using “Relation extensionality”:
    For any `x`, `y`:
        x ⦗ S ▷ Ran S ⦘ y
      =⟨ “Relationship via ▷” ⟩
        x ⦗ S ⦘ y ∧ y ∈ Ran S
      =⟨ “Membership in `Ran`” ⟩
        x ⦗ S ⦘ y ∧ (∃ z • z ⦗ S ⦘ y)
      =⟨ (3.60) ⟩
        x ⦗ S ⦘ y ≡ x ⦗ S ⦘ y ⇒ (∃ z • z ⦗ S ⦘ y)
      =⟨ Substitution ⟩
        x ⦗ S ⦘ y ≡ (z ⦗ S ⦘ y)[z ≔ x] ⇒ (∃ z • z ⦗ S ⦘ y) 
      =⟨ “∃-Introduction”, “Identity of ≡” ⟩
        x ⦗ S ⦘ y

Theorem (14.237) “Domain restriction via ⨾”:
    A ◁ R = id A ⨾ R
Proof:
  Using “Relation extensionality”:
    For any `x`, `y`:
        x ⦗ A ◁ R ⦘ y
      =⟨ “Relationship via ◁” ⟩
        x ∈ A ∧ x ⦗ R ⦘ y
      =⟨ Substitution ⟩   
        (z ∈ A ∧ z ⦗ R ⦘ y)[z ≔ x]  
      =⟨ “Trading for ∃” , “One-point rule for ∃” ⟩   
        ∃ z • z = x ∧ z ∈ A ∧ z ⦗ R ⦘ y  
      =⟨ “Relationship via `id`” ⟩ 
        ∃ z • x ⦗ id A ⦘ z ∧ z ⦗ R ⦘ y 
      =⟨ “Relation composition” ⟩ 
        x ⦗ id A ⨾ R ⦘ y

Theorem (14.238) “Domain antirestriction via ⨾”:
    A ⩤ R = id (~ A) ⨾ R
Proof:
    A ⩤ R
  =⟨ “Definition of ⩤ via ◁” ⟩
    ~ A ◁ R 
  =⟨ “Domain restriction via ⨾” ⟩  
    id (~ A) ⨾ R

Theorem (14.239) “Range restriction via ⨾”:
    R ▷ B = R ⨾ id B
Proof:
  Using “Relation extensionality”:
    For any `x`, `y`:
        x ⦗ R ▷ B ⦘ y
      =⟨ “Relationship via ▷” ⟩
        x ⦗ R ⦘ y ∧ y ∈ B
      =⟨ Substitution ⟩ 
        (x ⦗ R ⦘ z ∧ y ∈ B)[z ≔ y]  
      =⟨ “Trading for ∃” , “One-point rule for ∃” ⟩   
        ∃ z • x ⦗ R ⦘ z ∧ z = y ∧ y ∈ B  
      =⟨ “Relationship via `id`” ⟩ 
        ∃ z • x ⦗ R ⦘ z ∧ z ⦗ id B ⦘ y 
      =⟨ “Relation composition” ⟩ 
        x ⦗ R ⨾ id B ⦘ y

Theorem (14.240) “Range antirestriction via ⨾”:
    R ⩥ B = R ⨾ id (~ B)
Proof:
    R ⩥ B
  =⟨ “Definition of ⩥ via ▷” ⟩ 
    R ▷ ~ B
  =⟨ “Range restriction via ⨾” ⟩
    R ⨾ id (~ B) 

Theorem (14.241) “Switching ▷ and ◁ in ⨾”:
    (R ▷ B) ⨾ S  =  R ⨾ (B ◁ S)
Proof:
    (R ▷ B) ⨾ S
  =⟨ “Range restriction via ⨾” ⟩
    (R ⨾ id B) ⨾ S
  =⟨ “Associativity of ⨾” ⟩  
    R ⨾ (id B ⨾ S)
  =⟨ “Domain restriction via ⨾” ⟩
    R ⨾ (B ◁ S)

Theorem (14.242) “Switching ⩥ and ⩤ in ⨾”:
    (R ⩥ B) ⨾ S  =  R ⨾ (B ⩤ S)
Proof:
    (R ⩥ B) ⨾ S
  =⟨ “Range antirestriction via ⨾” ⟩
    (R ⨾ id (~ B)) ⨾ S
  =⟨ “Associativity of ⨾” ⟩  
    R ⨾ (id (~ B) ⨾ S)
  =⟨ “Domain antirestriction via ⨾” ⟩
    R ⨾ (B ⩤ S)

Theorem (14.243) “Mutual associativity of ◁ with ⨾”:
    (A ◁ R) ⨾ S  =  A ◁ (R ⨾ S)
Proof:
    (A ◁ R) ⨾ S
  =⟨ “Domain restriction via ⨾” ⟩
    (id A ⨾ R) ⨾ S
  =⟨ “Associativity of ⨾”, “Domain restriction via ⨾” ⟩
    A ◁ (R ⨾ S)  

Theorem (14.244) “Mutual associativity of ⩤ with ⨾”:
    (A ⩤ R) ⨾ S  =  A ⩤ (R ⨾ S)
Proof:
    (A ⩤ R) ⨾ S
  =⟨ “Domain antirestriction via ⨾” ⟩
    (id (~ A) ⨾ R) ⨾ S
  =⟨ “Associativity of ⨾”, “Domain antirestriction via ⨾” ⟩
    A ⩤ (R ⨾ S)     

Theorem (14.245) “Mutual associativity of ⨾ with ▷”:
    (R ⨾ S) ▷ C  =  R ⨾ (S ▷ C)
Proof:
    (R ⨾ S) ▷ C
  =⟨ “Range restriction via ⨾” ⟩
    (R ⨾ S) ⨾ id C
  =⟨ “Associativity of ⨾”, “Range restriction via ⨾” ⟩  
     R ⨾ (S ▷ C)

Theorem (14.246) “Mutual associativity of ⨾ with ⩥”:
    (R ⨾ S) ⩥ C  =  R ⨾ (S ⩥ C)
Proof:
    (R ⨾ S) ⩥ C
  =⟨ “Range antirestriction via ⨾” ⟩
    (R ⨾ S) ⨾ id (~ C)
  =⟨ “Associativity of ⨾”, “Range antirestriction via ⨾” ⟩  
     R ⨾ (S ⩥ C)

Theorem (14.247) “Nested ◁”:  A ◁ (B ◁ R) = (A ∩ B) ◁ R
Proof:
    (A ∩ B) ◁ R
  =⟨ “Definition of ◁”  ⟩
    R ∩ ((A ∩ B) × 𝐔)
  =⟨ “Distributivity of × over ∩” ⟩
    R ∩ ((A × 𝐔) ∩ (B × 𝐔))
  =⟨ “Associativity of ∩” ⟩
    (R ∩ (B × 𝐔)) ∩ (A × 𝐔)
  =⟨ “Definition of ◁” ⟩     
    (B ◁ R) ∩ (A × 𝐔)
  =⟨ “Definition of ◁” ⟩
    A ◁ (B ◁ R) 
    
Theorem (14.248) “Nested ⩤”:  A ⩤ (B ⩤ R) = (A ∪ B) ⩤ R
Proof:
    (A ∪ B) ⩤ R
  =⟨ “Definition of ⩤” ⟩
    R ∩ (~ (A ∪ B) × 𝐔)
  =⟨ “De Morgan for ∪” ⟩
    R ∩ ((~ A ∩ ~ B) × 𝐔)
  =⟨ “Distributivity of × over ∩” ⟩
    R ∩ ((~ A × 𝐔) ∩ (~ B × 𝐔))
  =⟨ “Definition of ⩤” ⟩ 
    (B ⩤ R) ∩ (~ A × 𝐔)
  =⟨ “Definition of ⩤” ⟩      
    A ⩤ (B ⩤ R) 

Theorem (14.249) “Nested ▷”:  (R ▷ A) ▷ B = R ▷ (A ∩ B)
Proof:
    R ▷ (A ∩ B)
  =⟨ “Definition of ▷” ⟩
    R ∩ (𝐔 × (A ∩ B))
  =⟨ “Distributivity of × over ∩” ⟩
    R ∩ ((𝐔 × A) ∩ (𝐔 × B))
  =⟨ “Definition of ▷” ⟩
    (R ▷ A) ∩ (𝐔 × B)
  =⟨ “Definition of ▷” ⟩
    (R ▷ A) ▷ B

Theorem (14.250) “Nested ⩥”:  (R ⩥ A) ⩥ B = R ⩥ (A ∪ B)
Proof:
    R ⩥ (A ∪ B)
  =⟨ “Definition of ⩥” ⟩
    R ∩ (𝐔 × ~ (A ∪ B))
  =⟨ “De Morgan for ∪” ⟩
    R ∩ (𝐔 × (~ A ∩ ~ B))
  =⟨ “Distributivity of × over ∩” ⟩
    R ∩ ((𝐔 × ~ A) ∩ (𝐔 × ~ B))
  =⟨ “Definition of ⩥” ⟩
    (R ⩥ A) ∩ (𝐔 × ~ B)
  =⟨ “Definition of ⩥” ⟩
    (R ⩥ A) ⩥ B

Theorem (14.251) “Monotonicity of ◁”:  A ⊆ B  ⇒  A ◁ R  ⊆  B ◁ R
Proof:
  Assuming `A ⊆ B`:
      A ◁ R
    =⟨ “Definition of ◁” ⟩ 
      R ∩ (A × 𝐔)
    ⊆⟨ “Monotonicity of ∩” with “Monotonicity of ×”
      with Assumption `A ⊆ B` ⟩ 
      R ∩ (B × 𝐔)
    =⟨ “Definition of ◁” ⟩ 
      B ◁ R 

Theorem (14.252) “Monotonicity of ◁”:  R ⊆ S  ⇒  A ◁ R  ⊆  A ◁ S
Proof:
  Assuming `R ⊆ S`:
      A ◁ R
    =⟨ “Definition of ◁” ⟩ 
      R ∩ (A × 𝐔)
    ⊆⟨ “Monotonicity of ∩” with Assumption `R ⊆ S` ⟩ 
      S ∩ (A × 𝐔)
    =⟨ “Definition of ◁” ⟩ 
      A ◁ S  

Theorem (14.253) “Monotonicity of ◁”:  A ⊆ B ∧ R ⊆ S  ⇒  A ◁ R ⊆ B ◁ S
Proof:
  Assuming `A ⊆ B`, `R ⊆ S`:
      A ◁ R
    ⊆⟨ “Monotonicity of ◁” with Assumption `A ⊆ B` ⟩ 
      B ◁ R
    ⊆⟨ “Monotonicity of ◁” with Assumption `R ⊆ S` ⟩ 
      B ◁ S

Theorem (14.254) “Monotonicity of ▷”:  A ⊆ B  ⇒  R ▷ A  ⊆  R ▷ B
Proof:
  Assuming `A ⊆ B`:
      R ▷ A
    =⟨ “Definition of ▷” ⟩ 
      R ∩ (𝐔 × A)
    ⊆⟨ “Monotonicity of ∩” with “Monotonicity of ×”
      with Assumption `A ⊆ B` ⟩ 
      R ∩ (𝐔 × B)
    =⟨ “Definition of ▷” ⟩ 
      R ▷ B 

Theorem (14.255) “Monotonicity of ▷”:  R ⊆ S  ⇒  R ▷ A  ⊆  S ▷ A
Proof:
  Assuming `R ⊆ S`:
      R ▷ A
    =⟨ “Definition of ▷” ⟩ 
      R ∩ (𝐔 × A)
    ⊆⟨ “Monotonicity of ∩” with Assumption `R ⊆ S` ⟩ 
      S ∩ (𝐔 × A)
    =⟨ “Definition of ▷” ⟩ 
      S ▷ A 

Theorem (14.256) “Monotonicity of ▷”:  R ⊆ S ∧ A ⊆ B  ⇒  R ▷ A ⊆ S ▷ B
Proof:
  Assuming `R ⊆ S`, `A ⊆ B`:
      R ▷ A
    ⊆⟨ “Monotonicity of ▷” with Assumption `R ⊆ S` ⟩ 
      S ▷ A
    ⊆⟨ “Monotonicity of ▷” with Assumption `A ⊆ B` ⟩ 
      S ▷ B

Theorem (14.257) “Antitonicity of ⩤”:  A ⊆ B  ⇒  B ⩤ R  ⊆  A ⩤ R
Proof:
  Assuming `A ⊆ B` and using with “Antitonicity of ~”:
      B ⩤ R
    =⟨ “Definition of ⩤ via ◁” ⟩
      ~ B ◁ R
    ⊆⟨ “Monotonicity of ◁” with Assumption `A ⊆ B` ⟩ 
      ~ A ◁ R
    =⟨ “Definition of ⩤ via ◁” ⟩
      A ⩤ R

Theorem (14.258) “Monotonicity of ⩤”:  R ⊆ S  ⇒  A ⩤ R  ⊆  A ⩤ S
Proof:
  Assuming `R ⊆ S`:
      A ⩤ R
    =⟨ “Definition of ⩤ via ◁” ⟩
      ~ A ◁ R
    ⊆⟨ “Monotonicity of ◁” with Assumption `R ⊆ S` ⟩ 
      ~ A ◁ S
    =⟨ “Definition of ⩤ via ◁” ⟩
      A ⩤ S

Theorem (14.259) “Antitonicity of ⩥”:  A ⊆ B  ⇒  R ⩥ B  ⊆  R ⩥ A
Proof:
  Assuming `A ⊆ B` and using with “Antitonicity of ~”:
      R ⩥ B
    =⟨ “Definition of ⩥ via ▷” ⟩
      R ▷ ~ B
    ⊆⟨ “Monotonicity of ▷” with Assumption `A ⊆ B` ⟩ 
      R ▷ ~ A
    =⟨ “Definition of ⩥ via ▷” ⟩
      R ⩥ A

Theorem (14.260) “Monotonicity of ⩥”:  R ⊆ S  ⇒  R ⩥ A  ⊆  S ⩥ A
Proof:
  Assuming `R ⊆ S`:
      R ⩥ A
    =⟨ “Definition of ⩥ via ▷” ⟩
      R ▷ ~ A
    ⊆⟨ “Monotonicity of ▷” with Assumption `R ⊆ S` ⟩ 
      S ▷ ~ A
    =⟨ “Definition of ⩥ via ▷” ⟩
      S ⩥ A

Declaration: _⦇_⦈ : (t₁ ↔ t₂) → set t₁ → set t₂

Axiom (14.301) “Definition of ⦇_⦈”, “Range of ◁”: R ⦇ A ⦈ = Ran (A ◁ R)

Theorem (14.302) “Relational image”:  y ∈ R ⦇ A ⦈  ≡  (∃ x ❙ x ∈ A • x ⦗ R ⦘ y)
Proof:
    y ∈ R ⦇ A ⦈
  =⟨ “Definition of ⦇_⦈” ⟩ 
    y ∈ Ran (A ◁ R)
  =⟨ “Membership in `Ran`” ⟩
    ∃ x • x ⦗ A ◁ R ⦘ y
  =⟨ “Relationship via ◁” ⟩
    ∃ x • x ∈ A ∧ x ⦗ R ⦘ y
  =⟨ “Trading for ∃” ⟩
    ∃ x ❙ x ∈ A • x ⦗ R ⦘ y

Theorem (14.303) “Relational image of ⨾”:  (R ⨾ S) ⦇ A ⦈ = S ⦇ R ⦇ A ⦈ ⦈
Proof:
  Using “Set extensionality”:
    For any `y`:
        y ∈ (R ⨾ S) ⦇ A ⦈
      =⟨ “Relational image” ⟩
        ∃ x ❙ x ∈ A • x ⦗ R ⨾ S ⦘ y
      =⟨ “Relation composition” ⟩
        ∃ x ❙ x ∈ A • (∃ z • x ⦗ R ⦘ z ∧ z ⦗ S ⦘ y)
      =⟨ “Nesting for ∃” ⟩
        ∃ x , z ❙ x ∈ A • x ⦗ R ⦘ z ∧ z ⦗ S ⦘ y
      =⟨ (8.19), “Nesting for ∃” ⟩  
        ∃ z • (∃ x ❙ x ∈ A • x ⦗ R ⦘ z ∧ z ⦗ S ⦘ y)
      =⟨ “Trading for ∃”, “Distributivity of ∧ over ∃” ⟩ 
        ∃ z ❙ (∃ x ❙ x ∈ A • x ⦗ R ⦘ z) • z ⦗ S ⦘ y
      =⟨ “Relational image” ⟩ 
        ∃ z ❙ z ∈ R ⦇ A ⦈ • z ⦗ S ⦘ y
      =⟨ “Relational image” ⟩
        y ∈ S ⦇ R ⦇ A ⦈ ⦈

Theorem (14.304) “Relational image under converse” “Domain of ▷”:
   Dom (R ▷ B) = R ˘ ⦇ B ⦈
Proof:
  Using “Set extensionality”:
    For any `y`:
        y ∈ R ˘ ⦇ B ⦈
      =⟨ “Relational image” ⟩
        ∃ x ❙ x ∈ B • x ⦗ R ˘ ⦘ y
      =⟨ “Relation converse” ⟩
        ∃ x ❙ x ∈ B • y ⦗ R ⦘ x
      =⟨ “Trading for ∃” ⟩
        ∃ x • x ∈ B ∧ y ⦗ R ⦘ x
      =⟨ “Relationship via ▷” ⟩ 
        ∃ x • y ⦗ R ▷ B ⦘ x
      =⟨ “Membership in `Dom`” ⟩
        y ∈ Dom (R ▷ B) 
    
Theorem (14.305) “Range of ⩤”: Ran (A ⩤ R) = R ⦇ ~ A ⦈
Proof:
  Using “Set extensionality”:
    For any `y`:
        y ∈ R ⦇ ~ A ⦈
      =⟨ “Relational image” ⟩
        ∃ x ❙ x ∈ ~ A • x ⦗ R ⦘ y
      =⟨ “Set complement”, “Trading for ∃” ⟩ 
        ∃ x • ¬ (x ∈ A) ∧ x ⦗ R ⦘ y
      =⟨ “Relationship via ⩤” ⟩ 
        ∃ x • x ⦗ A ⩤ R ⦘ y
      =⟨ “Membership in `Ran`” ⟩ 
        y ∈ Ran (A ⩤ R)

Theorem (14.306) “Domain of ⩥”:  Dom (R ⩥ B) = R ˘ ⦇ ~ B ⦈
Proof:
  Using “Set extensionality”:
    For any `y`:
        y ∈ R ˘ ⦇ ~ B ⦈
      =⟨ “Relational image” ⟩
        ∃ x ❙ x ∈ ~ B • x ⦗ R ˘ ⦘ y
      =⟨ “Relation converse” ⟩
        ∃ x ❙ x ∈ ~ B • y ⦗ R ⦘ x       
      =⟨ “Set complement”, “Trading for ∃” ⟩ 
        ∃ x • ¬ (x ∈ B) ∧ y ⦗ R ⦘ x
      =⟨ “Relationship via ⩥” ⟩ 
        ∃ x • y ⦗ R ⩥ B ⦘ x
      =⟨ “Membership in `Dom`” ⟩ 
        y ∈ Dom (R ⩥ B) 

Declaration: _⊕_ : (t₁ ↔ t₂) → (t₁ ↔ t₂) → (t₁ ↔ t₂)

Axiom (14.401) “Definition of ⊕”:   R ⊕ S  =  (Dom S ⩤ R) ∪ S

Theorem (14.402) “Relation override”:
    x ⦗ R ⊕ S ⦘ y  ≡  (¬ (x ∈ Dom S) ∧ x ⦗ R ⦘ y) ∨ x ⦗ S ⦘ y
Proof:
    x ⦗ R ⊕ S ⦘ y
  =⟨ “Definition of ⊕” ⟩
    x ⦗ (Dom S ⩤ R) ∪ S ⦘ y 
  =⟨ “Relation union” ⟩
    x ⦗ Dom S ⩤ R ⦘ y ∨ x ⦗ S ⦘ y
  =⟨ “Relationship via ⩤” ⟩
    (¬ (x ∈ Dom S) ∧ x ⦗ R ⦘ y) ∨ x ⦗ S ⦘ y

Theorem (14.403) “Left-distributivity of ⊕ over ∪”:  (Q ∪ R) ⊕ S = (Q ⊕ S) ∪ (R ⊕ S)
Proof:
    (Q ∪ R) ⊕ S
  =⟨ “Definition of ⊕” ⟩ 
    (Dom S ⩤ (Q ∪ R)) ∪ S
  =⟨ “Distributivity of ⩤ over relation union” ⟩
    ((Dom S ⩤ Q) ∪ (Dom S ⩤ R)) ∪ S 
  =⟨ “Idempotency of ∪” ⟩
    ((Dom S ⩤ Q) ∪ S) ∪ ((Dom S ⩤ R) ∪ S)
  =⟨ “Definition of ⊕” ⟩ 
    (Q ⊕ S) ∪ (R ⊕ S)
    
Theorem (14.404) “Domain of ⊕”:  Dom (R ⊕ S) = Dom R ∪ Dom S
Proof:
    Dom (R ⊕ S)
  =⟨ “Definition of ⊕” ⟩
    Dom ((Dom S ⩤ R) ∪ S)
  =⟨ “Domain of union” ⟩
    Dom (Dom S ⩤ R) ∪ Dom S
  =⟨ “Domain of ⩤” ⟩ 
    (Dom R - Dom S) ∪ Dom S
  =⟨ (11.53) ⟩ 
    Dom R ∪ Dom S
    
Theorem (14.405) “Associativity of ⊕”: (Q ⊕ R) ⊕ S = Q ⊕ (R ⊕ S)
Proof:
    (Q ⊕ R) ⊕ S 
  =⟨ “Definition of ⊕” ⟩ 
    (Dom S ⩤ (Q ⊕ R)) ∪ S 
  =⟨ “Definition of ⊕” ⟩
    (Dom S ⩤ ((Dom R ⩤ Q) ∪ R)) ∪ S
  =⟨ “Distributivity of ⩤ over relation union” ⟩ 
    (Dom S ⩤ (Dom R ⩤ Q)) ∪ (Dom S ⩤ R)  ∪ S
  =⟨ “Nested ⩤” ⟩  
    ((Dom R ∪ Dom S) ⩤ Q) ∪ ((Dom S ⩤ R) ∪ S)
  =⟨ “Domain of ⊕” ⟩ 
    (Dom (R ⊕ S) ⩤ Q) ∪ ((Dom S ⩤ R) ∪ S)
  =⟨ “Definition of ⊕” ⟩
    (Dom (R ⊕ S) ⩤ Q) ∪ (R ⊕ S) 
  =⟨ “Definition of ⊕” ⟩ 
     Q ⊕ (R ⊕ S)
     
Theorem (14.406) “Range of ⊕”:  Ran (R ⊕ S) = R ⦇ ~ (Dom S) ⦈ ∪ Ran S
Proof:
    Ran (R ⊕ S)
  =⟨ “Definition of ⊕” ⟩
    Ran ((Dom S ⩤ R) ∪ S)
  =⟨ “Range of union” ⟩
    Ran (Dom S ⩤ R) ∪ Ran S
  =⟨ “Range of ⩤” ⟩
    R ⦇ ~ (Dom S) ⦈ ∪ Ran S
